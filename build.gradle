buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:2.4'
    }
}

plugins {
    id "nebula.os-package" version "2.2.6"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'os-package-base'
apply plugin: 'com.bmuschko.docker-java-application'

sourceCompatibility = 1.8

group = 'com.scopely'
mainClassName = 'com.scopely.infrastructure.kinesis.KinesisVcr'

repositories {
    jcenter()
    mavenCentral()
}

ext.awsSdkVersion = '1.9.34'

configurations {
    compile.exclude group: 'commons-logging'
}

dependencies {
    compile ('com.amazonaws:amazon-kinesis-connectors:1.1.1') {
        // Exclude dependency on all of the SDK
        exclude group: 'com.amazonaws'
    }
    compile ('com.amazonaws:amazon-kinesis-client:1.2.1') {
        // Exclude dependency on all of the SDK
        exclude group: 'com.amazonaws'
    }
    compile "com.amazonaws:aws-java-sdk-s3:${project.ext.awsSdkVersion}"
    compile "com.amazonaws:aws-java-sdk-kinesis:${project.ext.awsSdkVersion}"
    compile "com.amazonaws:aws-java-sdk-dynamodb:${project.ext.awsSdkVersion}"
    compile "com.amazonaws:aws-java-sdk-cloudwatch:${project.ext.awsSdkVersion}"

    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'org.slf4j:slf4j-simple:1.7.12'
    compile 'com.intellij:annotations:12.0'

    // For parsing command-line options
    compile 'com.beust:jcommander:1.48'

    // testing dependencies
    testCompile 'junit:junit:4.11'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile 'org.mockito:mockito-all:1.10.7'
    testCompile 'org.codehaus.groovy:groovy-all:2.3.0'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'

//    compile 'ch.qos.logback:logback-classic:1.1.3'
    runtime 'org.codehaus.groovy:groovy:2.3.3'
    compile 'org.slf4j:jcl-over-slf4j:1.7.7'
}

docker {
    url = "https://192.168.59.103:2376"
    certPath = new File(System.getenv('DOCKER_CERT_PATH'))

    registryCredentials {
        username = project.hasProperty('dockerHubUsername') ? project.property('dockerHubUsername') : null
        password = project.hasProperty('dockerHubPassword') ? project.property('dockerHubPassword') : null
        email = project.hasProperty('dockerHubEmail') ? project.property('dockerHubEmail') : null
    }

    javaApplication {
        baseImage = 'anapsix/docker-oracle-java8'
        maintainer = 'Scopely'
        tag = "avram/scopely"
    }
}


ospackage {
    packageName project.name

    requires('oracle-java8-installer')

    into "/opt/scopely/${project.name}"
    from ("${project.buildDir}/scripts") {
        into 'bin'
    }

    from(project.jar.outputs.files) {
        into 'lib'
    }

    from(project.configurations.runtime) {
        into 'lib'
    }
}